OPTION, ECHO=FALSE; 
OPTION, INFO=FALSE;		//psdump and statdump are in time steps
OPTION, STATDUMPFREQ = 10;	//How often beam stats dumped to .stat.  
OPTION, PSDUMPFREQ = 300000000;
OPTION, AUTOPHASE=4;		//Always leave this on, unless doing a phase scan
Option, VERSION=20200;
Option, ENABLEHDF5=TRUE; 

Title, string="LCLS2";

//------------------------------------------------------------------------
//					Global Parameters

REAL rf_freq         	     = 187.0e6;        //RF frequency. (Hz)
REAL n_particles             = 5e4;            //Number of particles in simulation.
REAL beam_bunch_charge       = 100.0*1e-12;    //Charge of bunch. (C)
REAL beam_current            = rf_freq*beam_bunch_charge*1e6;

//Initial energy Calc
REAL Edes    = 1.4e-9;
REAL gamma   = (Edes+EMASS)/EMASS;
REAL beta    = sqrt(1-(1/gamma^2));
REAL P0      = gamma*beta*EMASS;

value , {Edes, P0}; 					

//Input components for LCLS2 - EIC area
//-------------------------------------------------------------------------------------
// Gun
//
// Cavity/RF field.
//
// L:           physical element length (real in m). Length (of field map) (m).
// VOLT:        field scaling factor (real). RF field magnitude (MV/m).
// FMAPFN:      field file name (string)
// ELEMEDGE:        physical start of the element on the floor (real in m)
// TYPE:        specifies "STANDING" (default), "TRAVELLING" or "SINGLE GAP" structure
// FREQ:        RF frequency of cavity (real in MHz). Resonance frequency.
// LAG:         cavity phase (radians)
//

REAL deg=PI/180.0;

GUN:    RFCavity, L = 0.199, VOLT = 20.0, ELEMEDGE = 0.0, TYPE = "STANDING",
        FMAPFN = "../../fieldmaps/rfgunb_187MHz.txt",
        FREQ = 187.0, LAG = 8.5*deg;

// Buncher field map has z = -0.179 to 0.179
// Element edge should be center of device
BUNCHER:    RFCavity, L = 0.358, VOLT = 1.8, ELEMEDGE = 0.809116, TYPE = "STANDING",
            FMAPFN = "../../fieldmaps/rfgunb_buncher.txt",
            FREQ = 1300.0, LAG = -88.5*deg;

//-------------------------------------------------------------------------------------
// Solenoids
//
// L:           Physcial element length (m)
// ELEMEDGE:    Physcial start of element (m)
// KS:          Solenoid strength (T/m)
// FMAPFM:      Field file (string)

// realbucking Field map has z = -2.5 to 2.5
// Element edge should be center of device
SOLBF:  Solenoid, L = 0.48, ELEMEDGE= -0.062, KS = 0.0,
        FMAPFN = "/fieldmaps/rfgunb_bucking.txt";

// newSOL Field map has z = -0.24 to 0.24
// Element edge should be center of device
SOL1:   Solenoid, L = 0.48, ELEMEDGE= 0.24653, KS = 0.056,
        FMAPFN = "../../fieldmaps/rfgunb_solenoid.txt";

SOL2:   Solenoid, L = 0.48, ELEMEDGE= 1.64581, KS = 0.03,
        FMAPFN = "../../fieldmaps/rfgunb_solenoid.txt";


EIC:  Line = (GUN, SOL1, BUNCHER, SOL2);


//Input components for LCLS2 - Cryomodule 1
//-------------------------------------------------------------------------------------
// Cryomodule 1
//
// Cavity/RF field.
//
// L:           physical element length (real in m). Length (of field map) (m).
// VOLT:        field scaling factor (real). RF field magnitude (MV/m).
// FMAPFN:      field file name (string)
// ELEMEDGE:        physical start of the element on the floor (real in m)
// TYPE:        specifies "STANDING" (default), "TRAVELLING" or "SINGLE GAP" structure
// FREQ:        RF frequency of cavity (real in MHz). Resonance frequency.
// LAG:         cavity phase (radians)
//

REAL lcav = 1.3836;

REAL d1 = 3.3428;
REAL d2 = d1 + lcav;
REAL d3 = d2 + lcav;
REAL d4 = d3 + lcav;
REAL d5 = d4 + lcav;
REAL d6 = d5 + lcav;
REAL d7 = d6 + lcav;
REAL d8 = d7 + lcav;
REAL deg = PI/180.0;

C1: RFCavity, L = 1.3836, VOLT = 11.0, ELEMEDGE = d1, TYPE = "STANDING",
    FMAPFN = "../../fieldmaps/L0B_9cell.txt",
    FREQ = 1.3e3, LAG = 8*deg;

C2: RFCavity, L = 1.3836, VOLT = 1.0E-5, ELEMEDGE = d2, TYPE = "STANDING",
    FMAPFN = "../../fieldmaps/L0B_9cell.txt",
    FREQ = 1.3e3, LAG = -9.0*deg;

C3: RFCavity, L = 1.3836, VOLT = 26.0, ELEMEDGE = d3, TYPE = "STANDING",
    FMAPFN = "../../fieldmaps/L0B_9cell.txt",
    FREQ = 1.3e3, LAG = -20.0*deg;

C4: RFCavity, L = 1.3836, VOLT = 24.0, ELEMEDGE = d4, TYPE = "STANDING",
    FMAPFN = "../../fieldmaps/L0B_9cell.txt",
    FREQ = 1.3e3, LAG = -18.0*deg;

C5: RFCavity, L = 1.3836, VOLT = 32.0, ELEMEDGE = d5, TYPE = "STANDING",
    FMAPFN = "../../fieldmaps/L0B_9cell.txt",
    FREQ = 1.3e3, LAG = 0.0*deg;

C6: RFCavity, L = 1.3836, VOLT = 32.0, ELEMEDGE = d6, TYPE = "STANDING",
    FMAPFN = "../../fieldmaps/L0B_9cell.txt",
    FREQ = 1.3e3, LAG = 0.0*deg;

C7: RFCavity, L = 1.3836, VOLT = 32.0, ELEMEDGE = d7, TYPE = "STANDING",
    FMAPFN = "../../fieldmaps/L0B_9cell.txt",
    FREQ = 1.3e3, LAG = 0.0*deg;

C8: RFCavity, L = 1.3836, VOLT = 32.0, ELEMEDGE = d8, TYPE = "STANDING",
    FMAPFN = "../../fieldmaps/L0B_9cell.txt",
    FREQ = 1.3e3, LAG = 10.0*deg;

CM1:  Line = (C1,C2,C3,C4,C5,C6,C7,C8);

DR1: DRIFT, L = 5.0, ELEMEDGE = 13.0; 

//--------------------------------------------------------------------------------
// Accelerator lines
Col:ECOLLIMATOR, L=15.0, ELEMEDGE=0.0, XSIZE=30E-3,
    YSIZE=30E-3, OUTFN="col.h5";

SC_INJ:  Line = (Col, EIC, CM1, DR1);
//-------------------------------------------------------------------------------------

//// Distribution definition
Dist:DISTRIBUTION, TYPE = FROMFILE,
                   FNAME = "opal_50k.txt",
                   EMITTED = TRUE,
                   EMISSIONMODEL = NONE,
                   EMISSIONSTEPS = 100,
                   EKIN=0;

//Dist:DISTRIBUTION, TYPE = GAUSS,
//                   SIGMAR = 0.000168,
//                   TRISE = 14.2e-12,
//                   TFALL = 14.2e-12,
//                   CUTOFFLONG = 4.0,
//                   NBIN = 5,
//                   EMISSIONSTEPS = 100,
//                   EMISSIONMODEL = ASTRA,
//                   EKIN = 0.5,
//                   EMITTED = TRUE, 
//                   WRITETOFILE = TRUE;


//-------------------------------------------------------------------------------------
// Define Field solvers
// The mesh sizes should be a factor of 2 for most efficient space charge (SC) calculation.

FS_SC: Fieldsolver, FSTYPE = FFT, 
                    MX = 32, MY = 32, MT = 32,
		            PARFFTX = false, 
		            PARFFTY = false, 
		            PARFFTT = true,
		            BCFFTX = open, 
		            BCFFTY = open, 
		            BCFFTT = open,
		            BBOXINCR = 1, 
		            GREENSF = INTEGRATED;

//-------------------------------------------------------------------------------------
// Beam Definition

BEAM1: BEAM, PARTICLE = ELECTRON,
       pc = P0, NPART = n_particles, BFREQ = rf_freq,
       BCURRENT = beam_current, CHARGE = -1;

//-------------------------------------------------------------------------------------
// Run beamline

TRACK, LINE = SC_INJ, BEAM = BEAM1, MAXSTEPS = 1900000, DT = {5.0e-13, 5.0e-12}, ZSTOP={0.2, 15.0};
RUN, METHOD = "PARALLEL-T", BEAM = BEAM1, FIELDSOLVER = FS_SC, DISTRIBUTION = Dist;
ENDTRACK;

Stop; 
Quit;
